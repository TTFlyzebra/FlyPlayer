apply plugin: 'com.android.application'

//获取当前编译时间
def getCreateTime() {
    def createTime = new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+08:00")).trim()
    return createTime
}

/**
 * 获取Git 版本号
 */
def getGitSHA() {
    def gitSHA = 'git rev-parse --short HEAD'.execute().text.trim()
    return gitSHA
}

/**
 * 获取Git 提交次数
 */
def getGitCommitCount() {
    def gitCommitCount = 'git rev-list --count HEAD'.execute().text.trim();
    return gitCommitCount;
}

android {
    compileSdkVersion 23

    defaultConfig {
        applicationId "com.jancar.player.music"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode gitCommitCount.toInteger()
        versionName "v1.0."+gitSHA
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    signingConfigs {
        release {
            storeFile file("../platform.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        debug {
            storeFile file("../platform.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        release {
            minifyEnabled true //混淆
            zipAlignEnabled true//Zipalign优化
            shrinkResources true// 移除无用的resource文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }

    //配置自定义打包名称
    applicationVariants.all { variant ->
        variant.outputs.all {
            def fileName
            if (variant.buildType.name.equals('release')) {
                fileName = "${applicationId}_release_${gitSHA}_${gitCommitCount}_${createTime}.apk"
            } else if (variant.buildType.name.equals('debug')) {
                fileName = "${applicationId}_debug_${gitSHA}_${gitCommitCount}_${createTime}.apk"
            }
            outputFileName = fileName;
        }
    }
}

dependencies {
    implementation project(path: ':flymedialibrary')
    implementation project(path: ':mp3id3library')
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    compileOnly 'com.android.support:appcompat-v7:23.+'
    compileOnly 'com.android.support:recyclerview-v7:23.+'
    compileOnly files('libs/classes.jar')
}
